#! /usr/local/bin/snobol4 -b
* ISSE - Incredibly (Simple|Stupid) Snobol4 Editor
* Author: Rafal M. Sulejman
*
* Standard (no readline)
*
* Usage:
* {startline}[,{endline}]{command_letter}
*
* Commands
* a - appends one line after selected line
* c - changes selected line
* d - deletes selected line
* i - inserts one line before selected line
* l - lists (showing tabs and EOLs) the selected range
* n - shows selected range with line numbers
* p - shows selected range
* q - quit (without saving)
* w - write (save changes)
*
        cmds = "acdilnpqw"
        nums = "0123456789$%."
        cmdp = ( span(nums) | "" ) . ln.start 
+              ( "," | "") 
+              ( span(nums) | "" ) . ln.end 
+              any(cmds) . cmd
        ln.last = 0
        fn = host(0)
        t = table()

opene   input('infile',1,,fn)
rdfil   curline = infile                        :f(all)
        ln.last = ln.last + 1
        t<ln.last> = curline                    :(rdfil)
all     output = ln.last

rd.cmd  cmdln = input
        cmdln "." = ln.cur
        cmdln "$" = ln.last
        cmdln "%" = 1 "," ln.last
        cmdln cmdp                              :f(rd.cmd)
        ln.cur = 0 + ln.start                   :($('cmd.' cmd))

cmd.c   terminal = t<ln.cur>
        t<ln.cur> = input                       :(rd.cmd)

cmd.d   ln.end = ln.last
        ln.last = ln.last - 1
loop.d  t<ln.cur> = t<ln.cur + 1>               :(incr)

cmd.a   ln.cur = ln.cur + 1
cmd.i   ln.last = ln.last + 1
        ln.tmp = ln.last
loop.i  t<ln.tmp> = t<ln.tmp - 1>
        ln.tmp = gt(ln.tmp,ln.cur) ln.tmp - 1   :s(loop.i)
        t<ln.cur> = input                       :(rd.cmd)

cmd.l
loop.l  ln.out = t<ln.cur> 
tabs.l  ln.out char(9) = "^I"                   :s(tabs.l)
        ln.out rpos(0) = "$"
        output = ln.out                         :(incr)

cmd.n
loop.n  output = ln.cur char(9) t<ln.cur>       :(incr)

cmd.p   
loop.p  output = t<ln.cur>                      :(incr)

cmd.w   output('outfile',2,,fn)
        ln.cur = 1
        ln.end = ln.last
loop.w  outfile = t<ln.cur>                     :(incr)
        
incr    ln.cur = gt(ln.end,ln.cur) ln.cur + 1   :s($('loop.' cmd))f(rd.cmd)

cmd.q   

end
